buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
        maven { url = 'https://dist.creeper.host/Sponge/maven' }
        maven { url = 'https://repo.spongepowered.org/maven' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'

import net.minecraftforge.gradle.common.task.SignJar
boolean isSponge = hasProperty("isSponge")
if (file('secrets.properties').exists()) {
    println "Loading parameters from secrets.properties"
    ext.secrets = new Properties()
    file('secrets.properties').withReader {ext.secrets.load(it)}
}

version = "${mc_version}-${mayor}.${noapi}.${minor}.${patch}${extra}"
group = "arekkuusu"
archivesBaseName = "OffHandCombat"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: "${mc_mappings_channel}", version: "${mc_mappings_version}"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file('run')

            mods {
                offhandcombat {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            mods {
                offhandcombat {
                    source sourceSets.main
                }
            }
        }
    }
}

task deobfJar(type: Jar) {
    from(sourceSets.main.output)
    archiveName = "${archivesBaseName}-${version}-deobf.${extension}"
}

artifacts {
    archives deobfJar
}

repositories {
    maven {
        url = 'https://dist.creeper.host/Sponge/maven'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
    if(isSponge) {
        compile 'org.spongepowered:mixin:0.8'
    }else{
        embed('org.spongepowered:mixin:0.8') {
            exclude module: 'launchwrapper'
        }
    }
}

jar {
    exclude "**/*.bat"
    exclude "**/*.psd"
    exclude "**/*.txt"
    exclude "**/*.d"
    manifest {
        attributes([
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConnector": "arekkuusu.offhandcombat.common.core.Connector",
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            // Forge
            'FMLCorePluginContainsFMLMod': 'true',
            'MixinConfigs': 'assets/offhandcombat/offhandcombat.mixins.json',
            'TweakOrder': '0',
            'ForceLoadAsMod': 'true'
        ])
    }
}

task signJar(type: SignJar, dependsOn: jar) {

    keyStore = project.ext.secrets.keyStore // This needs to be a path to the keystore file
    alias = project.ext.secrets.keyStoreAlias
    storePass = project.ext.secrets.keyStorePass
    keyPass = project.ext.secrets.keyStoreKeyPass
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

build.dependsOn signJar

jar.finalizedBy('reobfJar')

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    //noinspection GroovyAssignabilityCheck
    type 'jar'
    builtBy 'reobfJar'
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact reobfArtifact
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

apply plugin: 'org.spongepowered.mixin'

mixin {
    add sourceSets.main, "mixins.offhandcombat.refmap.json"
}

sourceSets {
    main {
        ext.refMap = "mixins.offhandcombat.refmap.json"
    }
}